// $antlr-format alignTrailingComments true, columnLimit 150, minEmptyLines 1, maxEmptyLinesToKeep 1, reflowComments false, useTab false
// $antlr-format allowShortRulesOnASingleLine false, allowShortBlocksOnASingleLine true, alignSemicolons hanging, alignColons hanging

grammar AstroExpr;

main
    : expr EOF
    ;

predicate
    : '[' expr ']'
    ;

expr
    : orExpr
    ;

primaryExpr
    : functionCall 
    | lambdaExpr
    | variableReference
    | '(' expr ')'
    | Literal
    | Number
    | 'false'
    | 'true'
    | Identifier
    ;

functionCall
    : variableReference '(' (expr ( ',' expr)*)? ')'
    ;

lambdaExpr
    : variableReference '=>' expr
    ;

unionExprNoRoot
    : Identifier
    | filterExpr
    ;

filterExpr
    : primaryExpr predicate?
    ;

orExpr
    : andExpr (OR andExpr)*
    ;

andExpr
    : equalityExpr (AND equalityExpr)*
    ;

equalityExpr
    : relationalExpr (('=' | '!=') relationalExpr)*
    ;

relationalExpr
    : additiveExpr (('<' | '>' | '<=' | '>=') additiveExpr)*
    ;

additiveExpr
    : multiplicativeExpr (('+' | '-') multiplicativeExpr)*
    ;

multiplicativeExpr
    : mapExpr (('*'|'/') mapExpr)*
    ;

mapExpr
    : unaryExprNoRoot ('.' unaryExprNoRoot)*
    ;

unaryExprNoRoot
    : '-'* unionExprNoRoot
    ;

variableReference
    : '$' Identifier
    ;

Number
    : Digits ('.' Digits?)?
    | '.' Digits
    ;

fragment Digits
    : ('0' ..'9')+
    ;

LPAR
    : '('
    ;

RPAR
    : ')'
    ;

LBRAC
    : '['
    ;

RBRAC
    : ']'
    ;

MINUS
    : '-'
    ;

PLUS
    : '+'
    ;

DOT
    : '.'
    ;

MUL
    : '*'
    ;


COMMA
    : ','
    ;


LESS
    : '<'
    ;

MORE_
    : '>'
    ;

LE
    : '<='
    ;

GE
    : '>='
    ;


APOS
    : '\''
    ;

QUOT
    : '"'
    ;

AND
    : 'and'
    ;

OR
    : 'or'
    ;
EQ
    : '='
    ;
NE
    : '!='
    ;
    
False
    : 'false'
    ;

True
    : 'true'
    ;

Literal
    : '"' ~'"'* '"'
    | '\'' ~'\''* '\''
    ;

Whitespace
    : (' ' | '\t' | '\n' | '\r')+ -> skip
    ;

fragment Letter
    : [a-zA-Z_]
    ;
fragment LetterOrDigit 
    : Letter | [0-9];
    
Identifier
    : Letter LetterOrDigit*
    ;

